apply plugin: 'com.android.application'
//apply plugin: 'jacoco'

// See the following regarding optimisation of gradle builds:
//   https://developer.android.com/studio/build/optimize-your-build.html
// See the following regarding migrating to gradle plugin version 3:
//   https://developer.android.com/studio/preview/features/new-android-plugin-migration.html

// The line 'maven { url 'https://maven.fabric.io/public' }' is to fix the error:
//     'Error: Failed to resolve: com.twitter.sdk.android:twitter:x.y.z'
// See https://github.com/firebase/FirebaseUI-Android/issues/392'
repositories {
    mavenLocal()
    flatDir {
        dirs 'libs'
    }
    maven { url 'https://maven.fabric.io/public' }
}

// minSdkVersion must be >= 17 to set supportsRtl=true
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    // Load the properties in local.properties, so we can add them to resources below
    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    defaultConfig {
        applicationId "uk.jumpingmouse.moviecompanion"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Add the required local.properties to the resources
        // OMDb API key
        resValue "string", "omdbapi_key", properties.getProperty("omdbapi.key", "")

        // See:
        //   https://developer.android.com/studio/preview/features/new-android-plugin-migration.html
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
            // disable crashlytics for debug builds
            //ext.enableCrashlytics = false
        }
        release {
            // Use the defaultConfig applicationId for the build type which is uploaded
            // to the Play Store.
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Flavour dimensions.
    // The order in which each dimension is listed determines its priority, from
    // highest to lowest, when Gradle merges variant sources and configurations.
    // Each product flavour must be assigned to one of the flavour dimensions.
    // Hence 'dev' and 'prod' are 'stage' flavours, while 'free' and 'admin' are
    // 'mode' flavours.
    flavorDimensions "stage", "mode"

    productFlavors {
        // If it is required to keep development (dev) data and production (prod)
        // data separate, create a new Firebase project for the 'dev' product
        // flavour (i.e. for the ...dev and ...dev.admin applicationIds).
        dev {
            dimension "stage"
            applicationIdSuffix '.dev'
            versionNameSuffix "-dev"

            // To avoid using legacy multidex, set minSdkVersion to 21 or higher.
            //minSdkVersion 21
            // The following configuration limits the "dev" flavour to using
            // English string resources and xxhdpi screen-density resources.
            resConfigs "en", "xxhdpi"
        }
        prod {
            dimension "stage"
        }
        free {
            dimension "mode"
            applicationIdSuffix ".free"
        }
        admin {
            dimension "mode"
            applicationIdSuffix ".admin"
            versionNameSuffix "-admin"
        }
    }
}

ext {
    // The compatible versions of firebase, firebase-ui and the support library are listed here:
    // https://github.com/firebase/FirebaseUI-Android/blob/master/constants.gradle
    SUPPORT_LIB_VERSION = '25.1.1'
    //SUPPORT_LIB_VERSION = '25.3.1'
    FIREBASE_VERSION = '10.2.0'
    //FIREBASE_VERSION = '10.2.6'
    FIREBASE_UI_VERSION = '1.2.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile "com.android.support:appcompat-v7:$SUPPORT_LIB_VERSION"
    compile "com.android.support:design:$SUPPORT_LIB_VERSION"
    compile "com.android.support:palette-v7:$SUPPORT_LIB_VERSION"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Firebase
    compile "com.google.firebase:firebase-core:$FIREBASE_VERSION"
    compile "com.google.firebase:firebase-auth:$FIREBASE_VERSION"
    compile "com.google.firebase:firebase-database:$FIREBASE_VERSION"
//    compile "com.google.firebase:firebase-crash:$FIREBASE_VERSION"
    // Firebase UI Auth
    compile "com.firebaseui:firebase-ui-auth:$FIREBASE_UI_VERSION"

    // Timber
    compile 'com.jakewharton.timber:timber:4.5.1'

    // OmdbApi
    compile project(':omdbapi')

    // test
    testCompile 'junit:junit:4.12'

    // androidTest
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

// This plugin reads the google-services.json file and is required for Firebase
apply plugin: 'com.google.gms.google-services'
